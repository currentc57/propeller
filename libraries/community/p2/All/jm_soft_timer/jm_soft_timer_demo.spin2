'' =================================================================================================
''
''   File....... jm_soft_timer_demo.spin2
''   Purpose.... Demonstrates software timer that runs inside Spin
''   Author..... Jon "JonnyMac" McPhalen
''               Copyright (c) 2020 Jon McPhalen
''               -- see below for terms of use
''   E-mail..... jon.mcphalen@gmail.com
''   Started....
''   Updated.... 30 NOV 2020
''
''   {$P2}
''
'' =================================================================================================


con { timing }

  CLK_FREQ = 200_000_000                                        ' system freq as a constant
  MS_001   = CLK_FREQ / 1_000                                   ' ticks in 1ms
  US_001   = CLK_FREQ / 1_000_000                               ' ticks in 1us

  BR_TERM  = 115_200                                            ' terminal baud rate

  _clkfreq = CLK_FREQ                                           ' set system clock


con { fixed io pins }

  RX1      = 63  { I }                                          ' programming / debug
  TX1      = 62  { O }

  SF_CS    = 61  { O }                                          ' serial flash
  SF_SCK   = 60  { O }
  SF_SDO   = 59  { O }
  SF_SDI   = 58  { I }

  SD_SCK   = 61  { O }                                          ' sd card
  SD_CS    = 60  { O }
  SD_SDO   = 59  { O }
  SD_SDI   = 58  { I }

  SDA1     = 57  { IO }                                         ' i2c (optional)
  SCL1     = 56  { O }


con { app io pins }


con

  #true,  ON, OFF
  #false, NO, YES


obj

' main                                                          ' * master Spin cog
  timer : "jm_soft_timer"                                       ' + software timer
  term  : "jm_fullduplexserial"                                 ' * serial IO for terminal

' * uses cog when loaded
' + runs in Spin interpreter cog (interrupt)


pub main() | last, state, t, k

  setup()

  timer.set_mode(timer.M_RST)                                   ' reset clock
  last := -1                                                    ' force initial refresh

  wait_for_terminal(true)

  state := timer.M_RUN                                          ' let clock run
  timer.set_mode(state)

  repeat
    t := timer.raw()                                            ' get current state of clock
    if (t <> last)                                              ' new value?
      term.tx(term.HOME)                                        ' refresh display
      dec2(t.byte[3], ":")
      dec2(t.byte[2], ":")
      dec2(t.byte[1], ".")
      dec2(t.byte[0], " ")
      last := t

    k := term.rxcheck()
    case k
      "r", "R" :                                                ' reset and run
        state := timer.M_RSTRUN
        timer.set_mode(state)
        state := timer.M_RUN

      "x", "X" :                                                ' reset and stop
        state := timer.M_RST
        timer.set_mode(state)

      32 { spacebar } :                                         ' togge run/hold
        if (state <> timer.M_RUN)
          state := timer.M_RUN
        else
          state := timer.M_HOLD
        timer.set_mode(state)


pub dec2(value, chaser)

  term.tx((value  / 10) + "0")
  term.tx((value // 10) + "0")
  term.tx(chaser)


pub setup()

  timer.start()                                                 ' install timer into Spin2 interpreter cog

  term.start(RX1, TX1, %0000, BR_TERM)                          ' start terminal io


pub wait_for_terminal(clear)

  ' Download to RAM with F10
  ' F12 to open PST
  ' Click [Enable]
  ' Press Enter

  term.rxflush()
  term.rx()
  if (clear)
    term.tx(term.CLS)


con { license }

{{

  Terms of Use: MIT License

  Permission is hereby granted, free of charge, to any person obtaining a copy of this
  software and associated documentation files (the "Software"), to deal in the Software
  without restriction, including without limitation the rights to use, copy, modify,
  merge, publish, distribute, sublicense, and/or sell copies of the Software, and to
  permit persons to whom the Software is furnished to do so, subject to the following
  conditions:

  The above copyright notice and this permission notice shall be included in all copies
  or substantial portions of the Software.

  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED,
  INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A
  PARTICULAR PURPOSE AND NON-INFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
  HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF
  CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE
  OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.

}}